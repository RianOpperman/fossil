ENTRY(_start)
SECTIONS {
	. = 2M;

	.bootstrap : {
		bootstrap.o (.text)
	}

	. += 2M;

	.text : AT(ADDR(.text) - 2M) {
		_code = .;
		*(.text)
		*(.rodata*)
		. = ALIGN(4096);
	}

	.data : AT(ADDR(.data) - 2M) {
		_data = .;
		*(.data)
		. = ALIGN(4096);
	}

	.eh_frame : AT(ADDR(.eh_frame) - 2M) {
		_ehframe = .;
		*(.eh_frame)
		. = ALIGN(4096);
	}

	.bss : AT(ADDR(.bss) - 2M) {
		_bss = .;
		*(.bss)
		/*
			Usually need to  include generated COMMON symbols
			  under kernel BSS section or use GCC's -fno-common
		*/
		*(COMMON)
		. = ALIGN(4096);
	}

	_end = .;

	/DISCARD/ : {
		*(.comment)
	}
}
